/*
 * The following example shows a trigger for the Cloud News event. 
 * 
 * It iterates through each event and checks whether the news is urgent through the Urgent__c field. 
 * 
 * If the news is urgent, the trigger creates a case to dispatch a news reporter and adds the event location to the 
 * case subject.
 */

// Trigger for listening to Cloud_News events.
trigger CloudNewsTrigger on Cloud_News__e (after insert) {
    // List to hold all cases to be created.
    List<Case> cases = new List<Case>();
    
    // The Group object represents a queue.
    // Get queue Id for case owner
    Group queue = [SELECT Id FROM Group WHERE Name='Regional Dispatch' AND Type='Queue'];
    
    // Iterate through each notification.
    for (Cloud_News__e event : Trigger.New) {
        if (event.Urgent__c == true) {
            // Create Case to dispatch new team.
            Case cs = new Case();
            cs.Priority = 'High';
            cs.Subject = 'News team dispatch to ' + event.Location__c;
            //Each case OwnerId is assigned to a queue ID.
            cs.OwnerId = queue.Id;
            cases.add(cs);
        }
   }
    
   // Insert all cases corresponding to events received.
   insert cases;
}